<?xml version="1.0" encoding="utf-8"?>
<!--
 *
 * The MIT License
 *
 * Copyright (c) 2008
 * United Nations Office at Geneva
 * Center for Advanced Visual Analytics
 * http://cava.unog.ch
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
-->
<mx:ComboBox xmlns:mx="http://www.adobe.com/2006/mxml"
	height="20"
	width="95%"
	color="#CCCCCC"
	prompt="Select edge annotation."
	toolTip="Select the label type of edges."
	initialize="initData()"
	change="changeEdgeLabelRenderer()" >
	
	<mx:ArrayCollection id="elvalues">
		<mx:String>Default</mx:String>
		<mx:String>Basic</mx:String>
		<mx:String>WFB</mx:String>
	</mx:ArrayCollection>
	
	<mx:Script>	
		<![CDATA[
			import mx.utils.ObjectUtil;
			import org.un.cava.birdeye.ravis.utils.LogUtil;
		
			import org.un.cava.birdeye.ravis.utils.events.VGraphEvent;
			import org.un.cava.birdeye.ravis.graphLayout.visual.VisualGraph;
			import org.un.cava.birdeye.ravis.components.renderers.edgeLabels.DefaultEdgeLabelRenderer;
			import org.un.cava.birdeye.ravis.components.renderers.edgeLabels.BaseEdgeLabelRenderer;
			import org.un.cava.birdeye.ravis.components.renderers.edgeLabels.WFBEdgeLabelRenderer;
			
		  private static const _LOG:String = "components.ui.controls.vgraphControls.EdgeLabelSelector";
			private var _vgraph:VisualGraph;

			/**
			 * Provides access to the registered vgraph object.
			 * */
			public function set vgraph(v:VisualGraph):void {
				_vgraph = v;
				registerListeners();
			}


			/**
			 * @private
			 * */
			public function get vgraph():VisualGraph {
				return _vgraph;
			}
			
			/**
			 * When enabling or disabling this component, we also
			 * perform some specific tasks.
			 * Attention do completely disable interaction
			 * there is also the mouseEnabled property.
			 * 
			 * @inheritDoc
			 * */
			override public function set enabled(e:Boolean):void {
				if(e == true) {
					this.setStyle("color",0x000000);
					this.alpha=1;
				} else {
					this.setStyle("color",0xCCCCCC);
					this.alpha=0.3;
				}
				/* call superclass (ComboBox) */
				super.enabled = e;
			}

			/**
			 * Set the selected EdgeLabelRenderer.
			 * */
			public function changeEdgeLabelRenderer():void {
				
				var elr:String;
				
				if(_vgraph == null) {
					LogUtil.warn(_LOG, "Cannot change EdgeLabelRenderer, no valid vgraph!");
					return;
				}
				
				/* get the currently selected EdgeLabelRenderer */
				elr = this.selectedItem as String;
				
				switch(elr) {
					case "Default":
						vgraph.edgeLabelRenderer = new ClassFactory(DefaultEdgeLabelRenderer);
						break;
					case "Basic":
						vgraph.edgeLabelRenderer = new ClassFactory(BaseEdgeLabelRenderer);
						break;
					case "WFB":
						vgraph.edgeLabelRenderer = new ClassFactory(WFBEdgeLabelRenderer);
						break;
					default:
						LogUtil.warn(_LOG, "Illegal EdgeLabelRenderer selected:"+elr);
						vgraph.edgeLabelRenderer = new ClassFactory(DefaultEdgeLabelRenderer);
						break;
				}
				_vgraph.draw();
			}
			
			/**
			 * Refresh the selector with the current EdgeLabelRenderer.
			 * */
			public function refreshSelector(e:VGraphEvent = null):void {
				
				var elrClassName:String;
				
				if(_vgraph == null) {
					LogUtil.warn(_LOG, "Cannot change EdgeLabelRenderer, no valid vgraph!");
					return;
				}
				
                if(!_vgraph || !_vgraph.edgeLabelRenderer)
                    return;
                
				elrClassName = ObjectUtil.getClassInfo(_vgraph.edgeLabelRenderer.newInstance()).name;
				elrClassName = elrClassName.replace(/org.un.cava.birdeye.ravis.components.renderers.edgeLabels::/,"");
				
				switch(elrClassName) {
					case "DefaultEdgeLabelRenderer":
						this.selectedIndex = 0;
						break;
					case "BaseEdgeLabelRenderer":
						this.selectedIndex = 1;
						break;
					case "WFBEdgeLabelRenderer":
						this.selectedIndex = 2;
						break;
					default:
						LogUtil.warn(_LOG, "Illegal ELR class received:"+elrClassName);
						break;
				}
			}
			
			/**
			 * initialise the selection data.
			 * It is a bit unclear, why this seems to be necessary.
			 * */
			private function initData():void {
				this.dataProvider = elvalues;
			}
			
			/**
			 * register the event listener 
			 * */
			private function registerListeners():void {
				_vgraph.addEventListener(VGraphEvent.VGRAPH_CHANGED,refreshSelector);	
			}
		]]>
	</mx:Script>
</mx:ComboBox>
